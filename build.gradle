plugins {
    id 'org.springframework.boot' version '3.2.4' apply false
    id 'io.spring.dependency-management' version '1.1.5' apply false
    id 'com.google.protobuf' version '0.9.4' apply false
}

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = '17'
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':community-server') {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-amqp'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        runtimeOnly 'org.postgresql:postgresql'
    }
}

project(':admin-server') {
    apply plugin: 'com.google.protobuf'

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-amqp'
        implementation 'net.devh:grpc-client-spring-boot-starter:3.1.0.RELEASE'
        implementation 'javax.annotation:javax.annotation-api:1.3.2'
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:3.25.1"
        }
        plugins {
            grpc {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.60.0'
            }
        }
        generateProtoTasks {
            all()*.plugins {
                grpc {}
            }
        }
    }
}

project(':profanity-server') {
    apply plugin: 'com.google.protobuf'

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'net.devh:grpc-server-spring-boot-starter:3.1.0.RELEASE'
        implementation 'javax.annotation:javax.annotation-api:1.3.2'
        runtimeOnly 'org.postgresql:postgresql'
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:3.25.1"
        }
        plugins {
            grpc {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.60.0'
            }
        }
        generateProtoTasks {
            all()*.plugins {
                grpc {}
            }
        }
    }
}
